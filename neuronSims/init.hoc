/*
    Astrocyte model
    Simulation scripts to compute Astrocyte dynamics from cable compartmental models implemented in NEURON simulation environment.
    Astrocyte works reliably on a biophysically detailed multi-compartmental 3D structure with ion channels in some or all compartments.

    Last updated 07-June-2017
    Developed by : Leonid Savtchenko
    DCEE, Institute of Neurology, London, UK
    Email: leonid.savtchenko@ucl.ac.uk
*/

load_file("nrngui.hoc")

load_file("Utils.hoc")
load_file("NanoDistrFromFile.hoc")

// UI
objref vBoxSelectGeometry, plotShapeGeometry
// Geometry file
objref fileGeometry

// Shows geometry.
proc showSelectedGeometry() {
    removeIfExists(fast_flush_list, plotShapeGeometry)
    plotShapeGeometry = new PlotShape(0)
    fast_flush_list.append(plotShapeGeometry)
    plotShapeGeometry.size(-40,40,-37.0217,37.0217)
    plotShapeGeometry.variable("v")
    plotShapeGeometry.view(-40, -37.0217, 80, 74.0433, 850, 300, 433.8, 401.5)
}

// Loads geometry file.
proc loadGeometryFile() {
    fileGeometry = new File()

    fileGeometry.chooser("r", "Load", "*.hoc", "Load", "Cancel", getcwd())
    if (fileGeometry.chooser()) {
        load_file(fileGeometry.getname)
        showSelectedGeometry()
    } else {
        // File wasn't selected
    }
}

// Close geometry window and show main program UI.
proc initMainProgram() {
    vBoxSelectGeometry.unmap()
    plotShapeGeometry.unmap()

    load_file("MainProgram.hoc")
    runMain()
}

// Shows geometry selection UI.
proc showSelectGeometryUi() {
    vBoxSelectGeometry = new VBox()
    vBoxSelectGeometry.intercept(1)
    {
        xpanel("")
        xlabel("========== Load astrocyte geometry files ==========")
        xlabel("Load basic astrocyte model with dendrites and soma.")
        xlabel("(Several examples can be found in 'Geometry' folder.)")
        xbutton("Select basic geometry", "loadGeometryFile()")
        xlabel("-----------------------------------------------------------------------------------------")
        xlabel("Load distribution of diameters for leaves and stalks")
        xlabel("obtained from the Nano program for Matlab.")
        xlabel("(Optional. Uniform distribution will be used instead.)")
        xbutton("Select diameter distribution for nano geometry", "loadDiamDistrFile()")
        xlabel("=============================================")
        xlabel("Astrocyte model with Protocols of computation:")
        xlabel("Frap, Electrical, Ca and Glu dynamics")
        xbutton("Start program", "initMainProgram()")
        xlabel("-----------------------------------------------------------------------------------------")
        xbutton("Quit", "quit()")
        xpanel(0)
    }
    vBoxSelectGeometry.intercept(0)
    vBoxSelectGeometry.map("Astrocyte model", 500, 300, -1, -1)
}

showSelectGeometryUi()